      subroutine readsing(uu, vv, nk, nt, nd, sdate)
c
c  Reads the daily Singapore data, returning u and v components 
c   of the velocity (m/s).
c
c  Output: uu(nk, nt, nd) - zonal velocity
c          vv(nk, nt, nd) - northward velocity
c
c            = -999. for no data.
c
c Input:  nk = 5 = no of levels (70, 50, 30, 20, 10 hPa)
c         nt = 2 = no of times/day (00 and 12 GMT)
c         nd =     no of days.
c         sdate  = time of first data, in format YYMMDD.
c
c  ------------------------------------------------------------------
c
      implicit none
      integer nk, nt, nd, sdate
      real uu(nk, nt, nd), 
     :     vv(nk, nt, nd)
c
      integer yy, mm, dd, tt
c
      integer jk, jt, jd, yr, mn, dy
c
      integer dir(5), vel(5)
c
      character*15 ifile
c
      real pion180, twopi, ang
c
c  1. Preliminaries.
c
  100 continue
      pion180 = 3.14159/180.
      twopi   = 2. * 3.14159
c
      do 103 jd = 1, nd
         do 102 jt = 1, nt
            do 101 jk = 1, nk
               uu(jk, jt, jd) = -999. 
               vv(jk, jt, jd) = -999. 
  101       continue
  102    continue
  103 continue
c
c  --------------------------------------------------------------------
c
c  2. Locate data file.
c
  200 continue
c
      if (sdate.LT.810101) then 
         write(*,*)'READSING: No data available before 1.1.81 '
         goto 500
      else
         yr = sdate/10000
         mn = (sdate-yr*10000)/100
         dy = (sdate-yr*10000-mn*100)
         write(*,*)yr,mn,dy
         write(ifile,'(a9,i2,a4)')'data/sing',yr,'.dat'
         open(27,file=ifile)
      endif
c
c  ----------------------------------------------------------------------
c
c  3. Read the data and convert to u and v.
c
  300 continue
 3001 format(4i2,5(1x,i3,i2))
c
      do 319 jd = 1, nd
         do 318 jt = 1, nt
c---
            read(27,3001)yy,mm,dd,tt,(dir(jk),vel(jk),jk=1,5)
c---
            if ((yy.NE.yr).OR.(mm.NE.mn).OR.(dd.NE.dy)) then
               write(10,*)'READSING: WRONG DAY!',yr,yy,mn,mm,dy,dd
            endif
c
c           N.B. Convert from Knots -> m/s and mathematical phase.
c
            do 303 jk = 1, nk
               if (dir(jk).LT.999) then
                  ang = twopi - float(dir(jk))*pion180
                  uu(jk, jt, jd) = 0.52 * float(vel(jk)) * sin(ang)
                  vv(jk, jt, jd) = 0.52 * float(vel(jk)) * cos(ang)
               endif
  303       continue
c
  318    continue
         dy = dy + 1
         if (mn.EQ.2) then
            if ((yr.EQ.84).OR.(yr.EQ.88)) then  
               if (dy.GT.29) then 
                  dy = 1
                  mn = mn + 1
               endif
            else
               if (dy.GT.28) then 
                  dy = 1
                  mn = mn + 1
               endif
            endif
         elseif ((mn.EQ.4).OR.(mn.EQ.6).OR.(mn.EQ.9).OR.(mn.EQ.11)) then 
            if (dy.GT.30) then 
               dy = 1
               mn = mn + 1
             endif
         else
            if (dy.GT.31) then 
               dy = 1
               mn = mn + 1
             endif
         endif
  319 continue
c
      close(27)
c
  500 continue
c
      end

